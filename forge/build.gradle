plugins {
    id 'net.minecraftforge.gradle' version '[6.0.24,6.2)'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
    id 'org.spongepowered.mixin' version '0.7.+'
}

dependencies {
    compileOnly project(":common")

    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    implementation('net.sf.jopt-simple:jopt-simple:5.0.4') { version { strictly '5.0.4' } }

    annotationProcessor "org.spongepowered:mixin:${mixin_annotation_version}:processor"

    implementation "mysticdrew:common-networking-forge:${common_network_version}"

    implementation(fg.deobf("me.shedaniel.cloth:cloth-config-forge:${project.cloth_version}"))

    compileOnly("mezz.jei:jei-${minecraft_version}-forge-api:${jei_version}")
    runtimeOnly("mezz.jei:jei-${minecraft_version}-forge:${jei_version}")
}

mixin {
    config "${mod_id}.mixins.json"
}

minecraft {
    mappings channel: "parchment", version: "${parchment_mappings_version}-${parchment_minecraft_version}"

    reobf = false

    copyIdeResources = true

    runs {
        configureEach {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
        }

        client {
            property 'forge.enabledGameTestNamespaces', mod_id
        }

        server {
            property 'forge.enabledGameTestNamespaces', mod_id
            args '--nogui'
        }

        gameTestServer {
            property 'forge.enabledGameTestNamespaces', mod_id
        }
    }
}
sourceSets.each {
    def dir = layout.buildDirectory.dir("sourcesSets/$it.name")
    it.output.resourcesDir = dir
    it.java.destinationDirectory = dir
}
