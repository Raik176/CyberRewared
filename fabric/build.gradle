plugins {
    id "fabric-loom" version "1.7-SNAPSHOT"
}

configurations.runtimeClasspath.incoming.afterResolve {
    configurations.runtimeClasspath.resolvedConfiguration.resolvedArtifacts.each { artifact ->
        if (artifact.name == "sponge-mixin") {
            println "\nUse this to enable mixin hot-reloading:"
            println "-javaagent:\"${artifact.file.absolutePath}\"\n"
        }
    }
}

loom {
    runs {
        client {
            client()

            programArgs("--username=Dev")
            vmArgs(
                "-XX:+AllowEnhancedClassRedefinition",
                "-javaagent:\"C:\\Users\\RHM\\scoop\\apps\\gradle\\current\\.gradle\\caches\\modules-2\\files-2.1\\net.fabricmc\\sponge-mixin\\0.15.3+mixin.0.8.7\\51ee0a44ab05f6fddd66b09e66b3a16904f9c55d\\sponge-mixin-0.15.3+mixin.0.8.7.jar\""
            )
        }
    }
    runConfigs.configureEach {
        ideConfigGenerated = true
    }
    accessWidenerPath = project(":common").loom.accessWidenerPath
}

dependencies {
    implementation project(path: ":common", configuration: "namedElements")

    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${parchment_minecraft_version}:${parchment_mappings_version}@zip")
    }

    modImplementation "mysticdrew:common-networking-fabric:${common_network_version}"

    modApi("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}"
    modImplementation "net.fabricmc:fabric-loader:${project.fabric_loader_version}"

    modCompileOnlyApi("mezz.jei:jei-${minecraft_version}-fabric-api:${jei_version}")
    modRuntimeOnly("mezz.jei:jei-${minecraft_version}-fabric:${jei_version}")

    modImplementation("com.terraformersmc:modmenu:${project.modmenu_version}")

    include modApi("teamreborn:energy:${tr_energy_version}")

    testImplementation "net.fabricmc:fabric-loader-junit:${project.fabric_loader_version}"
}

test {
    useJUnitPlatform()
}

fabricApi {
    configureDataGeneration()
}
